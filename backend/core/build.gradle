plugins {
    id 'java-library'
    id 'com.diffplug.spotless'
    id 'checkstyle'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform('org.junit:junit-bom:5.10.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.18.1').aosp()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    maxWarnings = 0
}

tasks.register('lint') {
    dependsOn 'spotlessApply', 'checkstyleMain', 'checkstyleTest'
}

tasks.register('format') {
    dependsOn 'spotlessApply'
}

